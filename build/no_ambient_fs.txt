#version 330 core
out vec4 FragColor;

in vec3 normalPos;
in vec3 worldSpacePos;

uniform vec3 cameraWorldSpacePos;

// Material Properties
uniform float ambientStr;
uniform vec3 Ka;

uniform vec3 Kd;
uniform vec3 Ks;
uniform float Ns;

// Light properties
uniform vec3 lightColor;
uniform vec3 lightPos;

// all of this is done in world space 

void main()
{    

    // blinn phong shading

    // Ambient
    vec3 ambient = ambientStr * Ka;

    // diffuse light 
    vec3 normalizedNormal = normalize(normalPos);
    vec3 lightDir = normalize(lightPos - worldSpacePos);
    float diff = max(dot(normalizedNormal, lightDir), 0.0);
    vec3 diffuse = diff * Kd;

    // specular light (blinn-phong)
    vec3 viewDir = normalize(cameraWorldSpacePos-worldSpacePos);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    float spec = pow(max(dot(normalizedNormal, halfwayDir), 0.0), Ns);
    vec3 specular = spec * Ks;


    vec3 finalColor = (ambient + diffuse + specular) * lightColor; 
    FragColor = vec4(finalColor,1);
}